setopt EXTENDED_HISTORY
setopt HIST_IGNORE_ALL_DUPS
setopt HIST_IGNORE_SPACE
setopt HIST_REDUCE_BLANKS
setopt PROMPT_SP
setopt PROMPT_SUBST
setopt SHARE_HISTORY

HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=$((HISTSIZE/2))
bindkey -e

{{- if ne "" .bitbucket_team }}
cdpath=( "${HOME}/src/bitbucket.org/{{ .bitbucket_team }}" ${cdpath} )
{{- end }}
{{- if ne "" .github_org }}
cdpath=( "${HOME}/src/github.com/{{ .github_org }}" ${cdpath} )
{{- end }}
{{- if ne "" .github_user }}
cdpath=( "${HOME}/src/github.com/{{ .github_user }}" ${cdpath} )
{{- end }}
cdpath=( . ${cdpath} )
fpath=( "${HOME}/.local/share/zsh-completions" '{{ .homebrew_prefix }}/share/zsh-completions' ${fpath} )
path=( '{{ .homebrew_prefix }}/sbin' ${path} )
if [[ -d '{{ .homebrew_prefix }}/opt/ruby' ]]; then
  path=( '{{ .homebrew_prefix }}/opt/ruby/bin' ${path} )
  path=( "$(gem env gemdir)/bin" ${path} )
fi
path=( ~/.local/bin "$(python3 -msite --user-base)/bin" ${path} )

autoload -Uz compinit && compinit
autoload -Uz +X bashcompinit && bashcompinit

autoload -U colors
colors

# autoload -Uz bracketed-paste-magic
# zle -N bracketed-paste bracketed-paste-magic

# autoload -Uz url-quote-magic
# zle -N self-insert url-quote-magic

for config_file (~/.zsh/lib/*.zsh); do
  source "${config_file}"
done

# export PROMPT='${SSH_CLIENT:+%m:}%F{2}$(abbreviated_path)%f%# '
export PROMPT_EOL_MARK='%F{1}‚èé%f'

export CLICOLOR=1
export EDITOR=nvim
export VISUAL=nvim

alias diff='command diff -W${COLUMNS} -x .terraform -d'
alias glow='command glow -w ${COLUMNS}'
alias sdiff='command sdiff -d -w${COLUMNS}'
alias tg-clean='find . -type d -name .terragrunt-cache -prune -exec rm -rf {} \;'
alias vi=nvim
alias vim=nvim

#comp_files=()
#comp_files+=({{ .homebrew_prefix }}/etc/bash_completion.d/npm)
##comp_files+=({{ .homebrew_prefix }}/etc/bash_completion.d/vagrant)
#
#for f in ${comp_files}; do
#  [[ -f "${f}" ]] && source "${f}"
#done

if [[ "${TERM_PROGRAM}" = Apple_Terminal ]]; then
  _update_terminal_cwd() {
    local url_path=''
    {
      local LC_CTYPE=C LC_ALL= byte i ord
      for ((i = 0; i < ${#PWD}; ++i)); do
        byte="${PWD[i]}"
        if [[ "${byte}" =~ [/._~A-Za-z0-9-] ]]; then
          url_path+="${byte}"
        else
          ord=$(([#16] #byte))
          url_path+="%${ord}"
        fi
      done
    }
    printf '\e]7;%s\a' "file://${HOST}${url_path}"
  }
  typeset -ag chpwd_functions
  if [[ -z ${chpwd_functions[(r)_update_terminal_cwd]} ]]; then
    chpwd_functions+=_update_terminal_cwd
  fi
  _update_terminal_cwd
fi

# Do at end.
if [[ -e {{ .homebrew_prefix }}/share/zsh-autosuggestions/zsh-autosuggestions.zsh ]]; then
  source {{ .homebrew_prefix }}/share/zsh-autosuggestions/zsh-autosuggestions.zsh
fi

# Do at end.
if [[ -e {{ .homebrew_prefix }}/share/zsh-fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh ]]; then
  source {{ .homebrew_prefix }}/share/zsh-fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh
  # Temporary fix:
  if [[ $(uname -a) = (#i)*darwin* ]] {
    typeset -gA FAST_HIGHLIGHT
    FAST_HIGHLIGHT[chroma-man]=
  }
elif [[ -e {{ .homebrew_prefix }}/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
  source {{ .homebrew_prefix }}/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

# Do at end.
if (( $+commands[starship] )); then
  eval "$(starship init zsh)"
fi

# Do at end.
if (( $+commands[direnv] )); then
  eval "$(direnv hook zsh)"
fi

# vim: ft=zsh.jinja2

#compdef terraform

local -a _terraform_commands opt_args
_terraform_commands=(
  $'apply:Create or update infrastructure'
  $'console:Try Terraform expressions at an interactive command prompt'
  $'destroy:Destroy previously-created infrastructure'
  $'fmt:Reformat your configuration in the standard style'
  $'force-unlock:Release a stuck lock on the current workspace'
  $'get:Install or upgrade remote Terraform modules'
  $'graph:Generate a Graphviz graph of the steps in an operation'
  $'import:Associate existing infrastructure with a Terraform resource'
  $'init:Prepare your working directory for other commands'
  $'login:Obtain and save credentials for a remote host'
  $'logout:Remove locally-stored credentials for a remote host'
  $'metadata:Metadata related commands'
  $'modules:Show all declared modules in a working directory'
  $'output:Show output values from your root module'
  $'plan:Show changes required by the current configuration'
  $'providers:Show the providers required for this configuration'
  $'query:Search and list remote infrastructure with Terraform'
  $'refresh:Update the state to match remote systems'
  $'show:Show the current state or a saved plan'
  $'stacks:Manage HCP Terraform stack operations'
  $'state:Advanced state management'
  $'taint:Mark a resource instance as not fully functional'
  $'test:Execute integration tests for Terraform modules'
  $'untaint:Remove the \'tainted\' state from a resource instance'
  $'validate:Check whether the configuration is valid'
  $'version:Show the current Terraform version'
  $'workspace:Workspace management'
)

__apply() {
  _arguments \
    $'-auto-approve[Skip interactive approval of plan before applying.]' \
    $'-backup=[(path) Path to backup the existing state file before modifying. Defaults to the "-state-out" path with ".backup" extension. Set to "-" to disable backup.]:file:_files' \
    $'-compact-warnings[If Terraform produces any warnings that are not accompanied by errors, show them in a more compact form that includes only the summary messages.]' \
    $'-destroy[Destroy Terraform-managed infrastructure.]' \
    $'-lock=[(true) Lock the state file when locking is supported.]:bool:(true false)' \
    $'-lock-timeout=[(0s) Duration to retry a state lock.]' \
    $'-input=[(true) Ask for input for variables if not directly set.]:bool:(true false)' \
    $'-no-color[If specified, output wil be colorless.]' \
    $'-parallelism=[(10) Limit the number of parallel resource operations.]' \
    $'-refresh=[(true) Update state prior to checking for differences. This has no effect if a plan file is given to apply.]:bool:(true false)' \
    $'-replace=[(resource) Terraform will plan to replace this resource instance instead of doing an update or no-op action.]' \
    $'-state=[(terraform.tfstate) Path to read and save state (unless state-out is specified).]:file:_files' \
    $'-state-out=[(path) Path to write state to that is different than "-state". This can be used to preserve the old state.]:file:_files' \
    $'-target=[(resource) Resource to target. Operation will be limited to this resource and its dependencies. This flag can be used multiple times.]:target:__statelist' \
    $'-var[("foo=bar") Set a variable in the Terraform configuration. This flag can be set multiple times.]' \
    $'-var-file=[(foo) Set variables in the Terraform configuration from a file. If "terraform.tfvars" or any ".auto.tfvars" files are present, they will be automatically loaded.]:file:_files'
}

__console() {
  _arguments \
    $'-plan[Create a new plan (as if running "terraform plan") and then evaluate expressions against its planned state, instead of evaluating against the current state. You can use this to inspect the effects of configuration changes that haven\'t been applied yet.]' \
    $'-state=[(path) Legacy option for the local backend only. See the local backend\'s documentation for more information.]:file:_files' \
    $'-var[("foo=bar") Set a variable in the Terraform configuration. This flag can be set multiple times.]' \
    $'-var-file=[(filename) Set variables in the Terraform configuration from a file. If "terraform.tfvars" or any ".auto.tfvars" files are present, they will be automatically loaded.]:file:_files'
}

__destroy() {
  _arguments \
    $'-auto-approve[Skip interactive approval before destroying.]' \
    $'-backup=[(path) Path to backup the existing state file before modifying. Defaults to the "-state-out" path with ".backup" extension. Set to "-" to disable backup.]:file:_files' \
    $'-force[Deprecated: same as auto-approve.]' \
    $'-lock=[(true) Lock the state file when locking is supported.]:bool:(true false)' \
    $'-lock-timeout=[(0s) Duration to retry a state lock.]' \
    $'-no-color[If specified, output will contain no color.]' \
    $'-parallelism=[(10) Limit the number of concurrent operations.]' \
    $'-refresh=[(true) Update state prior to checking for differences. This has no effect if a plan file is given to apply.]:bool:(true false)' \
    $'-state=[(terraform.tfstate) Path to read and save state (unless state-out is specified).]:file:_files' \
    $'-state-out=[(path) Path to write state to that is different than "-state". This can be used to preserve the old state.]:file:_files' \
    $'-target=[(resource) Resource to target. Operation will be limited to this resource and its dependencies. This flag can be used multiple times.]:target:__statelist' \
    $'-var[("foo=bar") Set a variable in the Terraform configuration. This flag can be set multiple times.]' \
    $'-var-file=[(foo) Set variables in the Terraform configuration from a file. If "terraform.tfvars" or any ".auto.tfvars" files are present, they will be automatically loaded.]:file:_files'
}

__fmt() {
  _arguments \
    $'-check[Check if the input is formatted. Exit status will be 0 if all input is properly formatted and non-zero otherwise.]' \
    $'-diff[Display diffs of formatting changes]' \
    $'-list=[(true) List files whose formatting differs (always false if using STDIN)]:bool:(true false)' \
    $'-no-color[If specified, output won\'t contain any color.]' \
    $'-recursive[Also process files in subdirectories. By default, only the given directory (or current directory) is processed.]' \
    $'-write=[(true) Write result to source file instead of STDOUT (always false if using STDIN or -check)]:bool:(true false)'
}

__force_unlock() {
  _arguments
}

__get() {
  _arguments \
    $'-no-color[If specified, output will contain no color.]' \
    $'-update=[(false) If true, modules already downloaded will be checked for updates and updated if necessary.]:bool:(true false)'
}

__graph() {
  _arguments \
    $'-draw-cycles[Highlight any cycles in the graph with colored edges. This helps when diagnosing cycle errors.]' \
    $'-no-color[If specified, output will contain no color.]' \
    $'-type=[(plan) Type of graph to output. Can be: plan, plan-destroy, apply, validate, input, refresh.]'
}

__import() {
  _arguments \
    $'-allow-missing-config[Allow import when no resource configuration block exists.]' \
    $'-backup=[(path) Path to backup the existing state file before modifying. Defaults to the "-state-out" path with ".backup" extension. Set to "-" to disable backup.]:file:_files' \
    $'-config=[(path) Path to a directory of Terraform configuration files to use to configure the provider. Defaults to pwd. If no config files are present, they must be provided via the input prompts or env vars.]:dir:_directories' \
    $'-input=[(true) Ask for input for variables if not directly set.]:bool:(true false)' \
    $'-lock=[(true) Lock the state file when locking is supported.]:bool:(true false)' \
    $'-lock-timeout=[(0s) Duration to retry a state lock.]' \
    $'-no-color[If specified, output will contain no color.]' \
    $'-provider=[(provider) Specific provider to use for import. This is used for specifying aliases, such as "aws.eu". Defaults to the normal provider prefix of the resource being imported.]' \
    $'-state=[(PATH) Path to the source state file. Defaults to the configured backend, or "terraform.tfstate"]:file:_files' \
    $'-state-out=[(PATH) Path to the destination state file to write to. If this is not specified, the source state file will be used. This can be a new or existing path.]:file:_files' \
    $'-var[("foo=bar") Set a variable in the Terraform configuration. This flag can be set multiple times. This is only useful with the "-config" flag.]' \
    $'-var-file=[(foo) Set variables in the Terraform configuration from a file. If "terraform.tfvars" or any ".auto.tfvars" files are present, they will be automatically loaded.]:file:_files'
}

__init() {
  _arguments \
    $'-backend=[(true) Configure the backend for this configuration.]:bool:(true false)' \
    $'-backend-config=[This can be either a path to an HCL file with key/value assignments (same format as terraform.tfvars) or a 'key=value' format. This is merged with what is in the configuration file. This can be specified multiple times. The backend type must be in the configuration itself.]' \
    $'-force-copy[Suppress prompts about copying state data. This is equivalent to providing a "yes" to all confirmation prompts.]' \
    $'-from-module=[Copy the contents of the given module into the target directory before initialization.]' \
    $'-get=[(true) Download any modules for this configuration.]:bool:(true false)' \
    $'-get-plugins=[(true) Download any missing plugins for this configuration.]:bool:(true false)' \
    $'-input=[(true) Ask for input if necessary. If false, will error if input was required.]:bool:(true false)' \
    $'-lock=[(true) Lock the state file when locking is supported.]:bool:(true false)' \
    $'-lock-timeout=[(0s) Duration to retry a state lock.]' \
    $'-no-color[If specified, output will contain no color.]' \
    $'-plugin-dir[Directory containing plugin binaries. This overrides all default search paths for plugins, and prevents the automatic installation of plugins. This flag can be used multiple times.]:dir:_directories' \
    $'-reconfigure[Reconfigure the backend, ignoring any saved configuration.]' \
    $'-upgrade=[(false) If installing modules (-get) or plugins (-get-plugins), ignore previously-downloaded objects and install the latest version allowed within configured constraints.]:bool:(true false)' \
    $'-verify-plugins=[(true) Verify the authenticity and integrity of automatically downloaded plugins.]:bool:(true false)'
}

__login() {
  _arguments
}

__logout() {
  _arguments
}

__metadata() {
  local -a __metadata_subcommands
  __metadata_subcommands=(
    $'functions:Show signatures and descriptions for the available functions'
  )
  _describe -t metadata 'metadata subcommands' __metadata_subcommands
}

__output() {
  _arguments \
    $'-json[If specified, machine readable output will be printed in JSON format]' \
    $'-module=[(name) If specified, returns the outputs for a specific module]' \
    $'-no-color[ If specified, output will contain no color.]' \
    $'-state=[(path) Path to the state file to read. Defaults to "terraform.tfstate".]:file:_files'
}

__plan() {
  _arguments \
    $'-destroy[() If set, a plan will be generated to destroy all resources managed by the given configuration and state.]' \
    $'-detailed-exitcode[() Return detailed exit codes when the command exits. This will change the meaning of exit codes to: 0 - Succeeded, diff is empty (no changes); 1 - Errored, 2 - Succeeded; there is a diff]' \
    $'-input=[(true) Ask for input for variables if not directly set.]:bool:(true false)' \
    $'-lock=[(true) Lock the state file when locking is supported.]:bool:(true false)' \
    $'-lock-timeout=[(0s) Duration to retry a state lock.]' \
    $'-module-depth=[(n) Specifies the depth of modules to show in the output. This does not affect the plan itself, only the output shown. By default, this is -1, which will expand all.]' \
    $'-no-color[() If specified, output will contain no color.]' \
    $'-out=[(path) Write a plan file to the given path. This can be used as input to the "apply" command.]:file:_files' \
    $'-parallelism=[(10) Limit the number of concurrent operations.]' \
    $'-refresh=[(true) Update state prior to checking for differences.]:bool:(true false)' \
    $'-state=[(statefile) Path to a Terraform state file to use to look up Terraform-managed resources. By default it will use the state "terraform.tfstate" if it exists.]:file:_files' \
    $'-target=[(resource) Resource to target. Operation will be limited to this resource and its dependencies. This flag can be used multiple times.]:target:__statelist' \
    $'-var[("foo=bar") Set a variable in the Terraform configuration. This flag can be set multiple times.]' \
    $'-var-file=[(foo) Set variables in the Terraform configuration from a file. If "terraform.tfvars" or any ".auto.tfvars" files are present, they will be automatically loaded.]:file:_files'
}

__providers() {
  _arguments
}

__refresh() {
  _arguments \
    $'-backup=[(path) Path to backup the existing state file before modifying. Defaults to the "-state-out" path with ".backup" extension. Set to "-" to disable backup.]:file:_files' \
    $'-input=[(true) Ask for input for variables if not directly set.]:bool:(true false)' \
    $'-lock=[(true) Lock the state file when locking is supported.]:bool:(true false)' \
    $'-lock-timeout=[(0s) Duration to retry a state lock.]' \
    $'-no-color[If specified, output will not contain any color.]' \
    $'-state=[(path) Path to read and save state (unless state-out is specified). Defaults to "terraform.tfstate".]:file:_files' \
    $'-state-out=[(path) Path to write state to that is different than "-state". This can be used to preserve the old state.]:file:_files' \
    $'-target=[(resource) A Resource Address to target. Operation will be limited to this resource and its dependencies. This flag can be used multiple times.]:target:__statelist' \
    $'-var[("foo=bar") Set a variable in the Terraform configuration. This flag can be set multiple times.]' \
    $'-var-file=[(path) Set variables in the Terraform configuration from a file. If "terraform.tfvars" is present, it will be automatically loaded if this flag is not specified.]:file:_files'
}

__show() {
  _arguments \
    $'-module-depth=[(n) The maximum depth to expand modules. By default this is zero, which will not expand modules at all.]' \
    $'-no-color[If specified, output will not contain any color.]'
}

__state() {
  local -a __state_subcommands
  __state_subcommands=(
    $'identities:List the identities of resources in the state'
    $'list:List resources in the state'
    $'mv:Move an item in the state'
    $'pull:Pull current state and output to stdout'
    $'push:Update remote state from a local state file'
    $'replace-provider:Replace provider in the state'
    $'rm:Remove instances from the state'
    $'show:Show a resource in the state'
  )
  _describe -t state 'state subcommands' __state_subcommands
}

__state_list() {
  _arguments \
    $'-id=[(id) Filters the results to include only instances whose resource types have an attribute named id whose value equals the given id string.]' \
    $'-state=[(path) Path to a Terraform state file to use to look up Terraform-managed resources. By default it will use the state "terraform.tfstate" if it exists.]:file:_files' \
    $'*:address:__statelist'
}

__state_mv() {
  _arguments \
    $'-backup=[(path) Path where Terraform should write the backup for the original state. This can\'t be disabled. If not set, Terraform will write it to the same path as the statefile with a ".backup" extension.]:file:_files' \
    $'-backup-out=[(path) Path where Terraform should write the backup for the destination state. This can\'t be disabled. If not set, Terraform will write it to the same path as the destination state file with a backup extension. This only needs to be specified if -state-out is set to a different path than -state.]:file:_files' \
    $'-dry-run[If set, prints out what would\'ve been moved but doesn\'t actually move anything.]' \
    $'-lock=[(true|false) Lock the state files when locking is supported.]:lock:(true false)' \
    $'-lock-timeout=[(seconds) Duration to retry a state lock.]' \
    $'-state=[(path) Path to the source state file. Defaults to the configured backend, or "terraform.tfstate"]:file:_files' \
    $'-state-out=[(path) Path to the destination state file to write to. If this isn\'t specified, the source state file will be used. This can be a new or existing path.]:file:_files' \
    $'::' \
    $':source:__statelist' \
    $':destination: '
}

__state_push() {
  _arguments \
    $'-force[Write the state even if lineages don\'t match or the remote serial is higher.]' \
    $'-lock=[(true|false) Lock the state file when locking is supported.]:lock:(true false)' \
    $'-lock-timeout=[(seconds) Duration to retry a state lock.]' \
    $'::' \
    $':destination:_files'
}

__state_rm() {
  _arguments \
    $'-backup=[(path) Path where Terraform should write the backup for the original state.]:file:_files' \
    $'-dry-run[If set, prints out what would\'ve been removed but doesn\'t actually remove anything.]' \
    $'-lock=[(true|false) Lock the state files when locking is supported.]:lock:(true false)' \
    $'-lock-timeout=[(seconds) Duration to retry a state lock.]' \
    $'-state=[(path) Path to the state file to update. Defaults to the current workspace state.]:file:_files' \
    $'*:address:__statelist'
}

__state_show() {
  _arguments \
    $'-state=[(path) Path to a Terraform state file to use to look up Terraform-managed resources. By default it will use the state "terraform.tfstate" if it exists.]:file:_files' \
    $'*:address:__statelist'
}

__statelist() {
  compadd $(terraform state list ${opt_args[-state]})
}

__taint() {
  _arguments \
    $'-allow-missing[If specified, the command will succeed (exit code 0) even if the resource is missing.]' \
    $'-backup=[(path) Path to backup the existing state file before modifying. Defaults to the "-state-out" path with ".backup" extension. Set to "-" to disable backup.]:file:_files' \
    $'-lock=[(true) Lock the state file when locking is supported.]:bool:(true false)' \
    $'-lock-timeout=[(0s) Duration to retry a state lock.]' \
    $'-module=[(path)  The module path where the resource lives. By default this will be root. Child modules can be specified by names. Ex. "consul" or "consul.vpc" (nested modules).]' \
    $'-no-color[If specified, output will not contain any color.]' \
    $'-state=[(path) Path to read and save state (unless state-out is  specified). Defaults to "terraform.tfstate".]:file:_files' \
    $'-state-out=[(path) Path to write updated state file. By default, the "-state" path will be used.]:file:_files' \
    $'*:address:__statelist'
}

__untaint() {
  _arguments \
    $'-allow-missing[If specified, the command will succeed (exit code 0) even if the resource is missing.]' \
    $'-backup=[(path) Path to backup the existing state file before modifying. Defaults to the "-state-out" path with ".backup" extension. Set to "-" to disable backup.]:file:_files' \
    $'-lock=[(true) Lock the state file when locking is supported.]:bool:(true false)' \
    $'-lock-timeout=[(0s) Duration to retry a state lock.]' \
    $'-module=[(path)  The module path where the resource lives. By default this will be root. Child modules can be specified by names. Ex. "consul" or "consul.vpc" (nested modules).]' \
    $'-no-color[If specified, output will not contain any color.]' \
    $'-state=[(path) Path to read and save state (unless state-out is  specified). Defaults to "terraform.tfstate".]:file:_files' \
    $'-state-out=[(path) Path to write updated state file. By default, the "-state" path will be used.]:file:_files'
}

__validate() {
  _arguments \
    $'-check-variables=[(true) If set to true (default), the command will check whether all required variables have been specified.]:bool:(true false)' \
    $'-no-color[If specified, output will not contain any color.]' \
    $'-var[("foo=bar") Set a variable in the Terraform configuration. This flag can be set multiple times.]' \
    $'-var-file=[(path) Set variables in the Terraform configuration from a file. If "terraform.tfvars" is present, it will be automatically loaded if this flag is not specified.]:file:_files'
}

__workspace() {
  local -a __workspace_subcommands
  __workspace_subcommands=(
    $'delete:Delete a workspace'
    $'list:List Workspaces'
    $'new:Create a new workspace'
    $'select:Select a workspace'
    $'show:Show the name of the current workspace'
  )
  _describe -t workspace 'workspace subcommands' __workspace_subcommands
}

_arguments \
  $'-chdir=[Switch to a different working directory before executing the given subcommand.]:dir:_directories' \
  $'-help[Show help output or the help for a specified subcommand.]' \
  $'-version[An alias for the "version" subcommand.]' \
  $'*:: :->command'

if (( CURRENT == 1 )); then
  _describe -t commands 'terraform command' _terraform_commands
  return
fi

local -a _command_args
case "$words[1]" in
  apply)
    __apply
    ;;
  console)
    __console
    ;;
  destroy)
    __destroy
    ;;
  fmt)
    __fmt
    ;;
  force-unlock)
    __force_unlock
    ;;
  get)
    __get
    ;;
  graph)
    __graph
    ;;
  import)
    __import
    ;;
  init)
    __init
    ;;
  login)
    __login
    ;;
  logout)
    __logout
    ;;
  metadata)
    test $CURRENT -lt 3 && __metadata
    [[ $words[2] = functions ]] && __metadata_functions # TODO
    ;;
  modules)
    __modules # TODO
    ;;
  output)
    __output
    ;;
  plan)
    __plan
    ;;
  providers)
    __providers
    ;;
  query)
    __query # TODO
    ;;
  refresh)
    __refresh
    ;;
  show)
    __show
    ;;
  stacks)
    __stacks # TODO
    ;;
  state)
    test $CURRENT -lt 3 && __state
    [[ $words[2] = list ]] && __state_list
    [[ $words[2] = mv ]] && __state_mv
    [[ $words[2] = push ]] && __state_push
    [[ $words[2] = rm ]] && __state_rm
    [[ $words[2] = show ]] && __state_show
    ;;
  taint)
    __taint
    ;;
  test)
    __test # TODO
    ;;
  untaint)
    __untaint
    ;;
  validate)
    __validate
    ;;
  workspace)
    test $CURRENT -lt 3 && __workspace ;;
esac
